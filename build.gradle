plugins {
    id 'java'
    id 'jacoco'  // Apply JaCoCo plugin
}

group = 'com.example'
version = '1.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

// Configure the global JaCoCo plugin
jacoco {
    toolVersion = "0.8.11"  // Set tool version globally
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.aspectj:aspectjrt:1.9.7'

    // JUnit dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
}

test {
    useJUnitPlatform()
    systemProperty "cucumber.junit-platform.naming-strategy", "long"
    finalizedBy jacocoTestReport
}



jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

task printJacocoPath {
    description = 'Prints the path to the JaCoCo agent JAR'
    group = 'help'

    doLast {
        def jacocoConfiguration = project.configurations.detachedConfiguration(
                project.dependencies.create("org.jacoco:org.jacoco.agent:${jacoco.toolVersion}"))
        def agentJar = jacocoConfiguration.find { it.name.contains('jacocoagent.jar') }
        if (agentJar) {
            println agentJar.absolutePath
        } else {
            println "JaCoCo agent JAR not found"
        }
    }
}